plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    compileSdk 34

    namespace "com.greylabsdev.pexwalls"
    defaultConfig {
        applicationId "com.greylabsdev.pexwalls"
        minSdk 24
        targetSdk 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }
}

configurations {
    ktlint
}

ext {
    room_version = "2.7.1"
    retrofit_version = "2.6.1"
    rxjava_version = "2.2.6"
    rxkotlin_version = "2.3.0"
    rxandroid_version = "2.1.0"
    rxbinding_version = "3.0.0"
    lifecycle_version = "2.1.0"
    koin_version = "4.0.3"
    timber_version = "4.7.0"
    logging_interceptor_version = "3.6.0"
    material_version = "1.0.0"
    contraint_version = "1.1.3"
    stetho_version = "1.5.1"
    navigation_version = "2.1.0"
    glide_version = "4.10.0"
    coroutines_android_version = "1.3.2"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.9.22"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.9.22"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // ktlint
    ktlint "com.pinterest:ktlint:0.36.0"

    // Room
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Logging-interceptor
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // RX
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "com.jakewharton.rxbinding3:rxbinding:$rxbinding_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    ksp "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Material
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$contraint_version"

    // Koin
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_android_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_android_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Stetho
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

}

task ktlint(type: JavaExec, group: "ktlint") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "ktlint") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}
